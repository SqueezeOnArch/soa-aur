# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Modified: Adrian Smith <triode1@btinternet.com>

pkgname=logitechmediaserver
pkgver=7.8.0
pkgrel=4
pkgdesc='Streaming audio server'
url='http://downloads.slimdevices.com/'
license=('GPL' 'custom')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
makedepends=('nasm' 'yasm' 'rsync' 'git')
[[ $CARCH = x86_64 ]] && optdepends=(lib32-{glibc,gcc-libs}': transcoding')
depends=('perl>5.19' 'perl<5.21')
source=("http://downloads.slimdevices.com/LogitechMediaServer_v${pkgver}/logitechmediaserver-${pkgver}.tgz"
        'git+https://github.com/Logitech/slimserver-vendor.git#branch=public/7.8'
        'service')
sha1sums=('9388eb200a48a3a04511e990686fee22a0d8bb5f'
          'SKIP'
          '734f84233fc7ab350b169f88aa5bb0cf3decad42')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	rm -r CPAN/arch Bin/{powerpc,sparc}-linux
	case $CARCH in
		x86_64) rm -r Bin/arm-linux ;;
		i686) rm -r Bin/arm-linux ;;
		arm*) rm -r Bin/i386-linux ;;
	esac
	sed \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-i ../slimserver-vendor/CPAN/buildme.sh
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	export CFLAGS=${CFLAGS//-O2/-Os}
	sh buildme.sh
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}"
	cp -a ../slimserver-vendor/CPAN/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/${pkgname}/CPAN"
	ln -s "/opt/${pkgname}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
