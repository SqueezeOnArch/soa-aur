# $Id$
# Maintainer: JSpaces <xpace -aT- incentre -d0T- net>
# Contributor Triode <triode1 -aT- btinternet -d0t- com>

_srcname=soxr
pkgname=lib${_srcname}
pkgver=0.1.1
pkgrel=2
pkgdesc="The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h') 
url="http://sourceforge.net/p/soxr/wiki/Home/"
license=('GPL')
depends=()
makedepends=('cmake')
source=(http://downloads.sourceforge.net/project/${_srcname}/${_srcname}-${pkgver}-Source.tar.xz
        soxr-c.patch)
sha256sums=('dcc16868d1a157079316f84233afcc2b52dd0bd541dd8439dc25bceb306faac2'
            'e93038626aa885494c78632607fc150813b9b4ee2e2773845f7f164fcaa5d02b')

prepare() {
  cd ${_srcname}-${pkgver}-Source
  # Patch pointed out by Triode. Thanks.
  msg2 "Applying patch from soxr git." 
  # Fix ineffectual SOXR_MINIMUM_PHASE and SOXR_INTERMEDIATE_PHASE in soxr_quality_spec recipe
  patch -Np1 -i ${srcdir}/soxr-c.patch
}

build() {
  cd ${_srcname}-${pkgver}-Source
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DVISIBILITY_HIDDENL=ON \
    -DWITH_AVFFT=ON \
    -DWITH_DOUBLE_PRECISION=ON \
    -DWITH_LSR_BINDINGS=ON \
    -DWITH_OPENMP=ON \
    -DWITH_PFFFT=ON \
    -DWITH_SIMD=ON \
    -DCMAKE_USE_RELATIVE_PATHS=ON \
    -DCMAKE_SKIP_RPATH=ON \
    ${srcdir}/${_srcname}-${pkgver}-Source
  make
}

package() {
  cd ${_srcname}-${pkgver}-Source
  
  make DESTDIR="${pkgdir}" install
  
  msg2 "Installing License."
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}-Source/LICENCE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  msg2 "Delete duplicate license in doc folder."
  rm -f "${pkgdir}"/usr/share/doc/${pkgname}/LICENCE
  }

# vim:set ts=2 sw=2 et:
