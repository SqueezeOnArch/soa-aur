# Contributor: Adrian Smith <triode1@btinternet.com>

pkgname=squeezelite
pkgver=r110.0b90637
pkgrel=1
pkgdesc='Lightweight headless squeezebox emulator'
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://code.google.com/p/squeezelite/'
license=(GPL3)
depends=('alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123' 'libsoxr')
provides=('squeezelite')
conflicts=('squeezelite')
source=('git+https://code.google.com/p/squeezelite/'
	'service'
	'conffile'
	'install'
	)

install=install

pkgver() {
         cd "$pkgname"
         printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd $pkgname

	export LDFLAGS="${LDFLAGS} -lasound -lpthread -lm -lrt"
	export CFLAGS="${CFLAGS} -DDSD -DRESAMPLE -DVISEXPORT -DLINKALL"
	make
}

package() {
	cd $pkgname

	# Main binary
	install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
	install -Dm644 ../conffile "${pkgdir}/etc/${pkgname}.conf"
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

sha256sums=('SKIP'
            'aa48c605671a6e24cb7a20ceb953ac39bc4f20fc102cde5c4d2f3f2019b96845'
            'f0753a1cbd0194119226587ff9c12257438674d9b8e0179d22f0d5461ad3a70a'
            'a09d70941893c0fbaead9677b1406a7d95b4756a8a81ce83ac6cf7578a304a3e')
