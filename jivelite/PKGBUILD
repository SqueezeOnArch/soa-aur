# Contributor: Adrian Smith <triode1@btinternet.com>

pkgname=jivelite
pkgver=r76.de07c79
pkgrel=1
pkgdesc='Squeezebox Control Application'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='http://github.com/ralph-irving/jivelite'
license=('BSD')
depends=('git' 'sdl' 'sdl_gfx' 'sdl_image' 'sdl_ttf')
provides=('jivelite')
conflicts=('jivelite')
source=('git+https://github.com/ralph-irving/jivelite/'
	'fb.modes')

pkgver() {
	 cd "$pkgname"
	 printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	 cd "$pkgname"

	 export LDFLAGS="${LDFLAGS} -lrt"
	 make PREFIX="/usr"
}

package() {
	 cd "$pkgname"

	 install -m0755 -D bin/jivelite "${pkgdir}/usr/bin/jivelite"
	 install -m0755 -D bin/jivelite "${pkgdir}/usr/bin/jivelite-sp"
	 install -d "${pkgdir}/usr/share/jive"
	 cp -a share/jive/* "${pkgdir}/usr/share/jive"
	 cp -a share/lua/5.1/* "${pkgdir}/usr/share/jive"
	 cp -a lib/lua/5.1/* "${pkgdir}/usr/share/jive"
	 install -Dm644 ../fb.modes "${pkgdir}/etc/fb.modes"
	 install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
	 install -Dm644 lib-src/lua-cjson-2.1.0/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/lua-cjson-2.1.0-LICENSE
	 install -Dm644 lib-src/luaexpat-1.2.0/doc/us/license.html "${pkgdir}"/usr/share/licenses/${pkgname}/luaexpat-1.2.0-license.html
	 install -Dm644 lib-src/luafilesystem-1.6.2/doc/us/license.html "${pkgdir}"/usr/share/licenses/${pkgname}/luafilesystem-1.6.2-license.html
	 install -Dm644 lib-src/luamd5/README "${pkgdir}"/usr/share/licenses/${pkgname}/luafilesystem-1.6.2-README
	 install -Dm644 lib-src/luasocket-2.0.2/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/luasocket-2.0.2-LICENSE
	 install -Dm644 lib-src/luazipfilter/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/luazipfilter-LICENSE
}

sha256sums=('SKIP'
            '1e4ac02deb66245788ad9c21cc16ecfad894e6efbc52a8fb473eeee1d6131fb7')
