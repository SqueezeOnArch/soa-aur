# Maintainer: Adrian Smith <triode1@btinternet.com>

pkgname=logitechmediaserver-cpan
pkgver=7.9
pkgrel=2
pkgdesc='Streaming audio server - cpan'
license=('GPL' 'custom')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('perl>5.19' 'perl<5.21')
makedepends=('nasm' 'yasm' 'rsync' 'git')
conflicts=('logitechediaserver')
source=('git+https://github.com/Logitech/slimserver-vendor.git#branch=public/7.9')
sha1sums=('SKIP')

pkgver() {
	cd "${srcdir}/slimserver-vendor"
	printf "7.9r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${srcdir}
	sed \
	    -e '/RUN_TESTS=1/c RUN_TESTS=0' \
	    -i slimserver-vendor/CPAN/buildme.sh
	sed \
	    -e '/tar -zcvf $OUTPUT.tgz $OUTPUT/s/^/#/' \
	    -e '/rm -rf $OUTPUT/s/^/#/' \
	    -i slimserver-vendor/flac/buildme-linux.sh
	sed \
	    -e '/tar -zcvf $OUTPUT.tgz $OUTPUT/s/^/#/' \
	    -e '/rm -rf $OUTPUT/s/^/#/' \
	    -i slimserver-vendor/faad2/buildme-linux.sh
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	export CFLAGS=${CFLAGS//-O2/-Os}
	sh buildme.sh
	cd "${srcdir}/slimserver-vendor/flac"
	sh buildme-linux.sh
	cd "${srcdir}/slimserver-vendor/faad2"
	sh buildme-linux.sh
}

package() {
	cd ${srcdir}
	install -d "${pkgdir}/opt/logitechmediaserver/CPAN"
	install -d "${pkgdir}/opt/logitechmediaserver/Bin"
	cp -a slimserver-vendor/CPAN/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/logitechmediaserver/CPAN"
	cp slimserver-vendor/flac/flac-build*/bin/flac "${pkgdir}/opt/logitechmediaserver/Bin"
	cp slimserver-vendor/faad2/faad2-build*/bin/faad "${pkgdir}/opt/logitechmediaserver/Bin"
}
