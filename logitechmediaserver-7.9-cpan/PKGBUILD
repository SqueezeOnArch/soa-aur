# Maintainer: Adrian Smith <triode1@btinternet.com>

pkgname=logitechmediaserver-cpan
pkgver=7.9r380.6f8c0bc
pkgrel=2
pkgdesc='Streaming audio server - cpan'
license=('GPL' 'custom')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('perl>5.19' 'perl<5.21' 'gd')
makedepends=('nasm' 'yasm' 'rsync' 'git')
conflicts=('logitechmediaserver')
source=('git+https://github.com/Logitech/slimserver-vendor.git#branch=public/7.9'
	'LICENSEs.txt')
sha1sums=('SKIP'
          '7532d2df0d4899c62f22150eea73693fcab0199b')

pkgver() {
	cd "${srcdir}/slimserver-vendor"
	printf "7.9r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${srcdir}
	sed \
	    -e '/RUN_TESTS=1/c RUN_TESTS=0' \
	    -i slimserver-vendor/CPAN/buildme.sh
	sed \
	    -e '/    build Media::Scan/c #    build Media::Scan' \
	    -i slimserver-vendor/CPAN/buildme.sh
}

build() {
	cd "${srcdir}/slimserver-vendor/CPAN"
	#export CFLAGS=${CFLAGS//-O2/-Os}
	sh buildme.sh
}

package() {
	cd ${srcdir}
	install -d "${pkgdir}/opt/logitechmediaserver/CPAN"
	install -d "${pkgdir}/opt/logitechmediaserver/Bin"
	cp -a slimserver-vendor/CPAN/build/5.20/lib/perl5/*linux*/* "${pkgdir}/opt/logitechmediaserver/CPAN"
	install -Dm644 LICENSEs.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSEs.txt
}
