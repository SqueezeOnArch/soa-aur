# Contributor: Adrian Smith <triode1@btinternet.com>

pkgname=soa-web
pkgver=r26.79a1c62
pkgrel=1
pkgdesc='Squeeze on Arch Web Interface'
arch=('any')
license=('GPL3')
depends=('turbolua' 'lua51-filesystem' 'ntfs-3g' 'cifs-utils' 'nfs-utils' 'samba')
provides=('soa-web')
conflicts=('soa-web')
source=('git+https://github.com/SqueezeOnArch/soa-web.git'
	'patch.patch'
	'service'
	'install'
	'smb.conf')

install=install

pkgver() {
	 cd "$pkgname"
	 printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	 cd "$pkgname"	
	 patch -p0 -i ../patch.patch
}

package() {
	 cd "$pkgname"
	 install -m0755 -D soa-web.lua "${pkgdir}/usr/bin/soa-web.lua"
	 install -d "${pkgdir}/usr/share/lua/5.1/soa-web"
	 install -d "${pkgdir}/usr/share/soa-web/templ"
	 install -d "${pkgdir}/usr/share/soa-web/static"
	 cp -a soa-web/* "${pkgdir}/usr/share/lua/5.1/soa-web"
	 cp -a templ/* "${pkgdir}/usr/share/soa-web/templ"
	 cp -a static/* "${pkgdir}/usr/share/soa-web/static"
	 install -d "${pkgdir}/etc/credentials"
	 install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
	 install -Dm644 ../smb.conf "${pkgdir}/etc/samba/soa-smb.conf"
}

sha256sums=('SKIP'
            '128e320a7f395d439424069dffbfeae8a7a06a19f138efc601408f012518b68f'
            'be852b6da5b2c20ca2ab25f4c3940e4e49061e85b12efb0188e2d97836d4dae6'
            'a6bf8245fa9f1711cf52b4e5b5761a2636518ce0a15118a3ee0091609b0c417c'
            '71e38376007c8784e27a92715d7a88061ff37a288bb0bd6aec506effc7044a84')
